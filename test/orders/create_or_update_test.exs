defmodule ExdeliveryTest.Orders.CreateOrUpdateTest do
  use ExUnit.Case

  import Exdelivery.Factory

  alias Exdelivery.Orders.CreateOrUpdate
  alias Exdelivery.Users.Agent, as: UserAgent

  describe "call/1" do
    setup do
      cpf = "12345678900"
      user = build(:user, cpf: cpf)

      Exdelivery.start_agents()

      UserAgent.save(user)

      item1 = %{
        category: :pizza,
        description: "Pizza de Queijo",
        quantity: 1,
        unity_price: "35.50"
      }

      item2 = %{
        category: :sobremesa,
        description: "Açaí",
        quantity: 1,
        unity_price: "15"
      }

      {:ok, user_cpf: cpf, item1: item1, item2: item2}
    end

    test "when all params are valid, saves the order", %{
      user_cpf: cpf,
      item1: item1,
      item2: item2
    } do
      params = %{user_cpf: cpf, items: [item1, item2]}

      response = CreateOrUpdate.call(params)

      assert {:ok, _uuid} = response
    end

    test "when there is no user with given cpf, returns an error", %{item1: item1, item2: item2} do
      params = %{user_cpf: "00000000000", items: [item1, item2]}

      response = CreateOrUpdate.call(params)

      expected_response = {:error, "User not found"}

      assert response == expected_response
    end

    test "when there are invalid items, return an error", %{
      user_cpf: cpf,
      item1: item1,
      item2: item2
    } do
      params = %{user_cpf: cpf, items: [%{item1 | quantity: 0}, item2]}

      response = CreateOrUpdate.call(params)

      expected_response = {:error, "invalid items"}

      assert response == expected_response
    end

    test "when there are no items, return an error", %{
      user_cpf: cpf
    } do
      params = %{user_cpf: cpf, items: []}

      response = CreateOrUpdate.call(params)

      expected_response = {:error, "Invalid parameters"}

      assert response == expected_response
    end
  end
end
